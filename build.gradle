buildscript {
	ext {
		springBootVersion = '1.4.1.RELEASE'
	}
	repositories {
		mavenCentral()
        jcenter()
		maven {
			url "https://plugins.gradle.org/m2/"
        }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.1"
		classpath 'se.transmode.gradle:gradle-docker:1.2'
	}
}

apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'org.sonarqube'
apply plugin: 'docker'

jar {
	baseName = 'self-training'
	version = '0.0.2-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "http://dl.bintray.com/basecrm/maven"
    }
}


dependencies {
	compile 'org.springframework.boot:spring-boot-starter-web'
	compile 'com.getbase:basecrm-java:1.4.4'
	compile 'org.projectlombok:lombok:1.16.10'

	testCompile 'org.spockframework:spock-spring:1.0-groovy-2.4'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'org.awaitility:awaitility:2.0.0'
	testCompile 'org.awaitility:awaitility-groovy:2.0.0'
}

test {
	outputs.upToDateWhen { false }
	systemProperties = System.properties
}

task logInfo {
	logging.captureStandardOutput LogLevel.INFO
}

bootRun {
	systemProperties = System.properties
}


task buildDocker (type: Docker, dependsOn: build) {
    group = 'build'
    description = 'Builds Docker container to run the workflow'

    push = false
    applicationName = jar.baseName
    version = jar.version
    dockerfile = file('src/main/resources/docker/Dockerfile')

    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}
