buildscript {
	ext {
		springBootVersion = '1.4.1.RELEASE'
	}
    repositories {
        mavenCentral()
        jcenter()
    }
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath 'se.transmode.gradle:gradle-docker:1.2'
	}
}

plugins {
    id "org.sonarqube" version "2.2"
}

apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'docker'
apply plugin: 'jacoco'

jar {
	baseName = 'self-training'
	version = '0.0.2-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "http://dl.bintray.com/basecrm/maven"
    }
}


dependencies {
    compile 'org.springframework.boot:spring-boot-starter-aop'
	compile 'org.springframework.boot:spring-boot-starter-hateoas'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
	compile 'com.getbase:basecrm-java:1.4.4'
	compile 'org.projectlombok:lombok:1.16.10'
	compile 'com.ryantenney.metrics:metrics-spring:3.1.3'

	testCompile 'org.spockframework:spock-spring:1.1-groovy-2.4-rc-2'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'org.awaitility:awaitility:2.0.0'
	testCompile 'org.awaitility:awaitility-groovy:2.0.0'
	testCompile 'cglib:cglib-nodep:3.2.4'
}

test {
	outputs.upToDateWhen { false }
    systemProperties = System.properties
}


task logInfo {
	logging.captureStandardOutput LogLevel.INFO
    logging.captureStandardError LogLevel.ERROR
}

bootRun {
	systemProperties = System.properties
}


task buildDocker (type: Docker, dependsOn: build) {
    group = 'build'
    description = 'Builds Docker container to run the workflow'

    push = false
    applicationName = jar.baseName
    version = jar.version
    dockerfile = file('src/main/resources/docker/Dockerfile')

    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}

sonarqube {
    properties {
        property "sonar.jacoco.reportPath", "build/jacoco/test.exec"
        property "sonar.groovy.jacoco.reportPath", "build/jacoco/test.exec"
    }
}

jacoco {
    toolVersion = "0.7.7.201606060606"
}

